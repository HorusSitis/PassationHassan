// canevas

// Caracteristique maillage cube 
pas_cube=step;

// Caracteristique maillage sphere
// xc=0.5;
// yc=0.5;
// zc=0.5;

// rayon variable
rayon = R;

//
pas_sphere=step;


///

// Numero (moins 1) du premier point definissant le cube
npc=100;
// Numero (moins 1) de la premiere ligne definissant le cube
nlc=200;
// Numero (moins 1) de la premiere Surface plane definissant le cube
nPSc=300;
// Numero (moins 1) de la premiere Surface definissant le cube
nsc=400;
// Numero de la surface totale definissant le cube
num_surf_loop_cube=600;
// Numero du volume
num_vol=500;
// Numero des surfaces physiques
num_phys=10;

/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
// Debut du maillage du cube 
/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
// Creation des 8 points du bord du cube
Point(npc+1) = {xmin,ymin,zmin,pas_cube};
Point(npc+2) = {xmax,ymin,zmin,pas_cube};
Point(npc+3) = {xmax,ymax,zmin,pas_cube};
Point(npc+4) = {xmin,ymax,zmin,pas_cube};
Point(npc+5) = {xmin,ymin,zmax,pas_cube};
Point(npc+6) = {xmax,ymin,zmax,pas_cube};
Point(npc+7) = {xmax,ymax,zmax,pas_cube};
Point(npc+8) = {xmin,ymax,zmax,pas_cube};

// Creation des 8 aretes du cube
Line(nlc+1) = {npc+4,npc+3};
Line(nlc+2) = {npc+3,npc+2};
Line(nlc+3) = {npc+2,npc+1};
Line(nlc+4) = {npc+1,npc+4};
Line(nlc+6) = {npc+5,npc+6};
Line(nlc+7) = {npc+6,npc+7};
Line(nlc+8) = {npc+7,npc+8};
Line(nlc+9) = {npc+8,npc+5};
Line(nlc+10) = {npc+1,npc+5};
Line(nlc+11) = {npc+4,npc+8};
Line(nlc+12) = {npc+2,npc+6};
Line(nlc+13) = {npc+3,npc+7};

// Definition du contour ferme et de la surface situee en zmin
Line Loop(nlc+14) = {nlc+3,nlc+4,nlc+1,nlc+2};
Plane Surface(nPSc+15) = {nlc+14};
// Definition du contour ferme et de la surface situee en zmax
Line Loop(nlc+16) = {nlc+6,nlc+7,nlc+8,nlc+9};
Plane Surface(nPSc+17) = {nlc+16};
// Definition du contour ferme et de la surface situee en xmin
Line Loop(nlc+18) = {nlc+10,-(nlc+9),-(nlc+11),-(nlc+4)};
Plane Surface(nPSc+19) = {nlc+18};
// Definition du contour ferme et de la surface situee en ymax
Line Loop(nlc+20) = {nlc+8,-(nlc+11),nlc+1,nlc+13};
Plane Surface(nPSc+21) = {nlc+20};
// Definition du contour ferme et de la surface situee en xmax
Line Loop(nlc+22) = {nlc+12,nlc+7,-(nlc+13),nlc+2};
Plane Surface(nPSc+23) = {nlc+22};
// Definition du contour ferme et de la surface situee en ymin
Line Loop(nlc+24) = {nlc+6,-(nlc+12),nlc+3,nlc+10};
Plane Surface(nPSc+25) = {nlc+24};

// // On impose la periodicite entre les surfaces d'equations xmin et xmax
Periodic Surface nPSc+23 {nlc+12,-(nlc+7), nlc+13, -(nlc+2)} = nPSc+19 {nlc+10, nlc+9, nlc+11, nlc+4};
// On impose la periodicite entre les surfaces d'equations ymin et ymax
Periodic Surface nPSc+21 {nlc+11,-(nlc+8), nlc+13, -(nlc+1)} = nPSc+25 {nlc+10, nlc+6, nlc+12, nlc+3};
// // On impose la periodicite entre les surfaces d'equations zmin et zmax
Periodic Surface nPSc+17 {-(nlc+8),-(nlc+7), -(nlc+6), -(nlc+9)} = nPSc+15 {nlc+1, nlc+2, nlc+3, nlc+4};


Surface Loop(num_surf_loop_cube) = {nPSc+17,-(nPSc+25),-(nPSc+23),-(nPSc+21),nPSc+19,nPSc+15};

/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
// Fin du maillage du cube 
/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
// Debut du maillage de la sphere 
/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
// Numero (moins 1) du premier point definissant la sphere
nps=700;
// Numero (moins 1) du premier cercle definissant la sphere
ncs=750;
// Numero (moins 1) de la premiere ligne definissant la sphere
nls=800;
// Numero (moins 1) de la premiere Surface definissant la sphere
nss=850;
// Numero de la surface totale definissant la sphere
num_surf_loop_sphere=900;
// Definition des points de construction de la sphere
Point(nps+1) = {xc,yc,zc,pas_sphere};
Point(nps+2) = {xc+rayon,yc,zc,pas_sphere};
Point(nps+3) = {xc,yc+rayon,zc,pas_sphere};
Point(nps+4) = {xc,yc,zc+rayon,pas_sphere};
Point(nps+5) = {xc-rayon,yc,zc,pas_sphere};
Point(nps+6) = {xc,yc-rayon,zc,pas_sphere};
Point(nps+7) = {xc,yc,zc-rayon,pas_sphere};

// Creation de 12 arcs de cercle
//Les arcs sur Gmsh sont definis a l'aide de la commande Circle. On utilise cette commande comme suit :
//Circle(numero attribue a l'arc circulaire) = {numero du point de depart, centre du cercle, point d'arrivee};
//On veut decouper la sphere en 8 surfaces, pour ensuite considerer le volume enferme a l'interieur du regroupement ces surfaces.
Circle(nps+1) = {nps+2,nps+1,nps+3};
Circle(nps+2) = {nps+3,nps+1,nps+5};
Circle(nps+3) = {nps+5,nps+1,nps+6};
Circle(nps+4) = {nps+6,nps+1,nps+2};
Circle(nps+5) = {nps+2,nps+1,nps+7};
Circle(nps+6) = {nps+7,nps+1,nps+5};
Circle(nps+7) = {nps+5,nps+1,nps+4};
Circle(nps+8) = {nps+4,nps+1,nps+2};
Circle(nps+9) = {nps+6,nps+1,nps+7};
Circle(nps+10) = {nps+7,nps+1,nps+3};
Circle(nps+11) = {nps+3,nps+1,nps+4};
Circle(nps+12) ={nps+4,nps+1,nps+6};


//Definition des contour de la sphere avec la commande Line Loop.
// Line Loop (numero du contour attribue au contours) = {arcs ou lignes a relier};
//Avec cette commande, il faut faire attention a l'orientation des lignes et des arcs. 
// Pour changer l'orientation d'une ligne ou d'un arc on rajoute le signe - avant son numero.
Line Loop(nls+1) = {nps+1,nps+11,nps+8};
Line Loop(nls+2) = {nps+2,nps+7,-(nps+11)};
Line Loop(nls+3) = {nps+3,-(nps+12),-(nps+7)};
Line Loop(nls+4) = {nps+4,-(nps+8),nps+12};
Line Loop(nls+5) = {nps+5,nps+10,-(nps+1)};
Line Loop(nls+6) = {-(nps+2),-(nps+10),nps+6};
Line Loop(nls+7) = {-(nps+3),-(nps+6),-(nps+9)};
Line Loop(nls+8) = {-(nps+4),nps+9,-(nps+5)};


// 5/ Surfaces :
//Surface : permet de definir des surfaces spheriques a partir des contours.
Ruled Surface(nss+1) = {nls+1};
Ruled Surface(nss+2) = {nls+2};
Ruled Surface(nss+3) = {nls+3};
Ruled Surface(nss+4) = {nls+4};
Ruled Surface(nss+5) = {nls+5};
Ruled Surface(nss+6) = {nls+6};
Ruled Surface(nss+7) = {nls+7};
Ruled Surface(nss+8) = {nls+8};

//Et Surface Loop : permet de definir la surface fermee engendree par les 8 surfaces definie ci-dessous.
Surface Loop (num_surf_loop_sphere) = {nss+1,nss+2,nss+3,nss+4,nss+5,nss+6,nss+7,nss+8};


/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
// Fin du maillage du cube 
/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
// Creation du bolume a mailler
/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
Volume(num_vol) = {num_surf_loop_cube,num_surf_loop_sphere};


/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
// Definition des surfaces physiques
/////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////
Physical Surface(num_phys+1) = {nPSc+15}; //surface situee en zmin
Physical Surface(num_phys+2) = {nPSc+17}; //surface situee en zmax
Physical Surface(num_phys+3) = {nPSc+19}; // surface situee en xmin
Physical Surface(num_phys+4) = {nPSc+21}; // surface situee en ymax
Physical Surface(num_phys+5) = {nPSc+23}; // surface situee en xmax
Physical Surface(num_phys+6) = {nPSc+25}; // surface situee en ymin
Physical Surface(num_phys+7) = {num_surf_loop_sphere}; // surface de la sphere
Physical Volume(num_phys+3) = {num_vol}; 
